pkgname=s6-linux-init
pkgver=1.1.2.0
pkgrel=2
pkgdesc='A tool to automate the creation of suitable stage 1 init binaries for s6-based Linux systems.'
arch=('x86_64')
url='https://skarnet.org/software/s6-linux-init/'
license=('ISC')
depends=('s6' 'libs6.so')
provides=('libs6_linux_init.so')
source=("https://skarnet.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	"rc.init"
	"rc.shutdown"
	"rc.shutdown.final"
	"runlevel")
sha256sums=('11ae08d0a66210b5e56b6baee0f6b9b1b69fbedb05fda1285b115a31ca46c77f'
            'ae9c38cf9e557105dafdc0a1848ebaf959d393f8a7ed0493af2ac57f52b1c041'
            '962df203bbfd9a4e46af509baa86684fe2987968a7cde9902e859f6ccf89aa77'
            '524df97d15ea7cbcd885dd9a2e5af1f6411c41614e5c8125807412cc2e07c65a'
            'c960e44cef24015b49d95d9532502cba34cab3610fe93f775c362d128838c3c8')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --skeldir=/etc/s6-linux-init/skel \
              --enable-shared \
              --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm755 ${srcdir}/rc.init "${pkgdir}"/etc/s6-linux-init/skel/rc.init
  install -Dm755 ${srcdir}/rc.shutdown "${pkgdir}"/etc/s6-linux-init/skel/rc.shutdown
  install -Dm755 ${srcdir}/rc.shutdown.final "${pkgdir}"/etc/s6-linux-init/skel/rc.shutdown.final
  install -Dm755 ${srcdir}/runlevel "${pkgdir}"/etc/s6-linux-init/skel/runlevel

  # execute s6-linux-init-maker for PM tracking
  ./s6-linux-init-maker -1 -f "${pkgdir}"/etc/s6-linux-init/skel -G "/usr/bin/agetty -L -8 tty12 115200" -c /etc/s6-linux-init/current "${pkgdir}"/etc/s6-linux-init/current
  mv "${pkgdir}"/etc/s6-linux-init/current/bin/init "${pkgdir}"/etc/s6-linux-init/current/bin/s6-init
  cp -a "${pkgdir}"/etc/s6-linux-init/current/bin "${pkgdir}"/usr
  rm -r "${pkgdir}"/etc/s6-linux-init/current/bin
}

